extends "res://ui/popout_panel.gd"

var open
var group: ButtonGroup

@onready var stage = get_tree().get_first_node_in_group("system:stage")
@onready var popup_bg = %Bg
@onready var panels = %Panels
@onready var onscreen_buttons = %OnscreenButtons
@onready var popup_btn = %PopoutBtn

func _ready() -> void:
	visible = false
	group = %ParameterBtn.button_group
	group.pressed.connect(
		func (button):
			if button.get_meta("panel") == null:
				return
			if button != null and button.button_pressed:
				var panel = button.get_node(button.get_meta("panel"))
				_open_panel(panel)
			else:
				_close_panel()
	)
	
	on_popout_requested.connect(
		func ():
			# get_window().unresizable = true
			onscreen_buttons.popout(true)
			self.size = Vector2i(580, 720)
			group.allow_unpress = false
			popup_bg.show()
			popup_btn.hide()
			_close_panel()
	)
	
	on_popout.connect(
		func ():
			var curr = open
			if curr == null:
				curr = panels.get_child(0)
			open = null
			
			_open_panel(curr)
	)
	
	on_restore.connect(
		func ():
			self.size = get_parent_area_size()
			group.allow_unpress = true
			popup_bg.hide()
			for p in panels.get_children():
				if p is Control:
					p.offset_right = p.size.x
			onscreen_buttons.restore()
			open == null
			_clear_buttons()
			popup_btn.show()
			# get_window().unresizable = false
	)
	
	var stage = get_tree().get_first_node_in_group("system:stage")
	if stage:
		stage.item_added.connect(
			func (_item):
				_clear_buttons()
		)
		stage.model_changed.connect(
			func (_model):
				_clear_buttons()
		)

func _close_panel():
	if open != null:
		var t: Tween = open.create_tween()
		t.tween_property(
			open,
			"offset_right",
			open.size.x,
			0.3
		).set_ease(Tween.EASE_IN_OUT).set_trans(Tween.TRANS_CUBIC).from(0)
		t.tween_callback(open.hide)
		t.tween_callback(open.teardown)
		
		open = null

func _open_panel(panel):
	if panel != null:
		# ignore opening panels that require a model when a model isn't present
		if panel.get_meta("model", false) and stage.active_model == null:
			return
			
	if panel == open:
		return
	
	_close_panel()
	if panel != null:
		panel.show()
		await panel.setup()
		panel.create_tween().tween_property(
			panel,
			"offset_right",
			0,
			0.3
		).set_ease(Tween.EASE_IN_OUT).set_trans(Tween.TRANS_CUBIC).from(panel.size.x)
	
	open = panel
	
func _clear_buttons():
	for b in group.get_buttons():
		b.button_pressed = false
	_close_panel()

func load_settings(settings: Dictionary):
	super.load_settings(settings)
	
	var do_popup = settings.get("popout_controls", false)
	if do_popup:
		await get_tree().process_frame
		self.popout()
	
func save_settings(settings: Dictionary):
	super.save_settings(settings)
	
	settings["popout_controls"] = self.is_floating
	
func _unhandled_input(event: InputEvent) -> void:
	if event.is_action_pressed("toggle_ui"):
		visible = !visible
		get_viewport().set_input_as_handled()

func _on_screenshot_btn_pressed() -> void:
	var stage = get_tree().get_first_node_in_group("system:stage")
	stage.get_node("ModelLayer")
	await Screenshot.snap(stage.get_viewport())
	
var editor: Window
func _on_action_btn_pressed() -> void:
	if editor != null:
		if editor.is_queued_for_deletion():
			editor = null
		else:
			editor.grab_focus()
			return
	if stage.active_model == null:
		return
	
	editor = preload("res://studio/hud/blueprint_editor/editor.tscn").instantiate()
	editor.active_model = stage.active_model
	editor.visible = true
	add_child(editor)
	
